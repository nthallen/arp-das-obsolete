Program : Gl_Statement* .

Gl_Statement :
  Assignment ';' /
  FunctionDef /
  Definition /
  Param_Modifier /
  ArrayAssignment ';' /
  Fit /
  ControlStructure .

Fit : 'Fit' UseIdent 'To' Expression ';' .

FunctionDef : 'Function' FuncDefIdent FunctionRange .
FunctionRange : '(' Prototype ')' Function_Body .
Function_Body :
  '{'
      Function_Statement*
      Return_Statement
  '}' /
  '=' Expression ';' .

Function_Statement :
  Assignment ';' /
  FunctionDef /
  Definition /
  Param_Modifier /
  'File' String ';' .

Assignment : DefLValue '=' Expression .
ArrayAssignment : '[' DeclLValue_List ']' '=' FunctionInvocation .
Return_Statement : 'Return' Expression ';' .
DeclLValue_List : DeclLValue_List_ .
DeclLValue_List_ : DeclLValue_List_Elt / DeclLValue_List_ ',' DeclLValue_List_Elt .
DeclLValue_List_Elt : DeclLValue .

Prototype : Prototype_Declarations .
Prototype_Declarations :
  Full_Declaration /
  Prototype_Declarations ',' Full_Declaration /
  Prototype_Declarations ',' DeclLValue_List_Elt .
Full_Declaration : Func_Arg_Type DeclLValue_List_Elt .

Definition : Arg_Type Def_List ';' .
Def_List : Def_Item // ',' .
Def_Item : DeclLValue / Def_Assignment .
Def_Assignment : LVDefIdent '=' Expression .

Func_Arg_Type : Arg_Type / 'INDEPENDENT' .
Arg_Type : 'PARAM' / 'INPUT' / 'CONSTANT' .

DefIdent : Ident .
UseIdent : Ident .
FuncDefIdent : DefIdent .
FuncUseIdent : UseIdent .
LVUseIdent : UseIdent .
LVDefIdent : DefIdent .
UseLValue  : LVUseIdent / UseIndex .
DefLValue  : LVDefIdent / UseIndex .
DeclLValue : LVDefIdent / DefIndex .
UseIndex : UseIdent '[' Integer ']' .
DefIndex : DefIdent '[' Integer ']' .

ControlStructure :
  'While' '(' ArrayAssignment ')' '{' Gl_Statement* '}' .
FunctionInvocation : FuncUseIdent '(' Func_Arg_List ')' .
Func_Arg_List : Func_Arg // ',' .
Func_Arg : String / Expression .

Param_Assign : UseLValue '=' Expression .
Param_Modifier :
  'Fix' Fix_List Fix_Condition ';' /
  'Float' UseLValue_List Fix_Condition ';' /
  'Constrain' UseLValue Constrain_Op Expression ';' /
  'Initialize' Param_Assign ';' .
Fix_List : ( UseLValue / Param_Assign ) // ',' .
Fix_Condition : / 'if' Logical_Expression .

UseLValue_List : UseLValue_List_ .
UseLValue_List_ : UseLValue_List_Elt / UseLValue_List_ ',' UseLValue_List_Elt .
UseLValue_List_Elt : UseLValue .

Constrain_Op : '<=' / '>=' .
Logical_Expression :
  Logical_Expression '&&' Logical_Primary /
  Logical_Expression '||' Logical_Primary /
  Logical_Primary /
  Expression Rel_Op Expression .
Logical_Primary :
  '(' Logical_Expression ')' /
  '!' Logical_Primary .

Rel_Op : '>' / '<' / '>=' / '<=' / '==' / '!=' .

Expression :
  Sum / '-' Sum / '+' Sum .
Sum :
  Sum '+' Term /
  Sum '-' Term /
  Term .
Term :
  Term '*' Factor /
  Term '/' Factor /
  Factor .
Factor :
  Primary '^' Primary /
  Primary .
Primary :
  '(' Expression ')' /
  UseLValue /
  Constant /
  VectorConstant /
  FunctionInvocation .

Constant : Integer / Float .
VectorConstant : '[' VectorList ']' .
VectorList : VectorElement // ',' .
VectorElement :
  Expression /
  Expression ':' Expression /
  Expression ':' Expression ':' Expression .
