Program : Gl_Statement* .

Gl_Statement :
  Assignment ';' /
  ArrayAssignment ';' /
  FunctionDef /
  Definition /
  Fit /
  ControlStructure .

Fit : 'Fit' UseIdent 'To' Expression ';' .

FunctionDef : 'Function' DefIdent FunctionRange .
FunctionRange : '(' Prototype_Declarations ')' Function_Body .
Function_Body :
  '{'
      Function_Statement*
      Return_Statement
  '}' /
  '=' Expression ';' .

Function_Statement :
  Assignment ';' /
  FunctionDef /
  Definition /
  'File' String ';' /
  Param_Modifier .

Assignment : DefLValue '=' Expression .
ArrayAssignment : '[' DefLValue_List ']' '=' FunctionInvocation .
Return_Statement : 'Return' Expression ';' .
DefLValue_List : DefLValue_List_ .
DefLValue_List_ : DefLValue_List_Elt / DefLValue_List_ ',' DefLValue_List_Elt .
DefLValue_List_Elt : DeclLValue .

Prototype_Declarations :
  Full_Declaration /
  Prototype_Declarations ',' Full_Declaration /
  Prototype_Declarations ',' DeclLValue .
Full_Declaration : Func_Arg_Type DeclLValue .

Definition : Arg_Type Def_List ';' .
Def_List : Def_Item // ',' .
Def_Item : DeclLValue / Assignment .
DeclLValue : DefLValue DeclIndex .
DeclIndex : / '[' Integer ']' .


Func_Arg_Type : Arg_Type / 'INDEPENDENT' .
Arg_Type : 'PARAM' / 'INPUT' / 'CONSTANT' .

DefIdent : Ident .
UseIdent : Ident .
UseLValue : UseIdent IndexExpr .
DefLValue : DefIdent .

IndexExpr : / '[' Expression ']' .

ControlStructure :
  'While' '(' ArrayAssignment ')' '{' Gl_Statement* '}' .
FunctionInvocation : UseIdent '(' Func_Arg_List ')' .
Func_Arg_List : Func_Arg // ',' .
Func_Arg : String / Expression .

Param_Assign : UseLValue '=' Expression .
Param_Modifier :
  'Fix' Fix_List Fix_Condition ';' /
  'Float' UseLValue_List Fix_Condition ';' /
  'Constrain' UseLValue Constrain_Op Expression ';' /
  'Initialize' Param_Assign ';' .
Fix_List : ( UseLValue / Param_Assign ) // ',' .
Fix_Condition : / 'if' Logical_Expression .

UseLValue_List : UseLValue_List_ .
UseLValue_List_ : UseLValue_List_Elt / UseLValue_List_ ',' UseLValue_List_Elt .
UseLValue_List_Elt : UseLValue .

Constrain_Op : '<=' / '>=' .
Logical_Expression :
  Logical_Expression '&&' Logical_Primary /
  Logical_Expression '||' Logical_Primary /
  Logical_Primary /
  Expression Rel_Op Expression .
Logical_Primary :
  '(' Logical_Expression ')' /
  '!' Logical_Primary .

Rel_Op : '>' / '<' / '>=' / '<=' / '==' / '!=' .

Expression :
  Sum / '-' Sum / '+' Sum .
Sum :
  Sum '+' Term /
  Sum '-' Term /
  Term .
Term :
  Term '*' Factor /
  Term '/' Factor /
  Factor .
Factor :
  Primary '^' Primary /
  Primary .
Primary :
  '(' Expression ')' /
  UseLValue /
  Constant /
  VectorConstant /
  FunctionInvocation .

Constant : Integer / Float .
VectorConstant : '[' VectorList ']' .
VectorList : VectorElement // ',' .
VectorElement :
  Expression /
  Expression ':' Expression /
  Expression ':' Expression ':' Expression .
